buildscript {
    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/public/'
        }
        maven {
            url "https://maven.aliyun.com/repository/gradle-plugin"
        }
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.beryx.jlink' version '3.0.0'
}

group 'io.vanstudio'
version '1.1.1'

repositories {
    maven {
        url 'https://maven.aliyun.com/repository/public/'
    }
    mavenCentral()
    maven {
        url "https://maven.aliyun.com/repository/gradle-plugin"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'io.vanstudio.srt'
    mainClass = 'io.vanstudio.srt.Main'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
    modularity.inferModulePath.set(false)
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics']
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher{
        name = 'SrtEdit'
        jvmArgs = ['-Dlog4j.configurationFile=./log4j2.xml','-Djava.locale.providers=HOST,CLDR,COMPAT']
    }
    jpackage {
        icon = 'src/main/resources/icons/srte.icns'
    }
}

dependencies {
    implementation("com.google.code.gson:gson:2.9.1")
    implementation("org.apache.httpcomponents:httpclient:4.5.14")
    implementation 'commons-codec:commons-codec:1.16.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

